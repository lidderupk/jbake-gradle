buildscript {
  repositories {
    maven { url "http://oss.jfrog.org/artifactory/repo" }
    jcenter()
  }
  dependencies {
    classpath 'me.champeau.gradle:jbake-gradle-plugin:0.2'
    classpath "org.jbake:jbake-core:2.3.2"
    classpath 'org.asciidoctor:asciidoctorj:1.5.2'
    classpath 'com.bluepapa32:gradle-watch-plugin:0.1.4' // watch files and perform action

    classpath 'com.github.jengelman.gradle.plugins:gradle-processes:0.3.0'
  }
}

apply plugin: 'me.champeau.jbake'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'com.bluepapa32.watch'

jbake {
  input = file 'src/jbake'
  clearCache = true
}

watch {
  jbake {
    files fileTree(dir: 'src')
    tasks 'jbake'
  }
}

task publish(dependsOn: 'jbake', type: GradleBuild) {
  System.setProperty('jna.nosys', 'true')
  buildFile = 'publish.gradle'
  tasks = ['publishGhPages']
}

idea {
  project {
    jdkName "1.8"
    languageLevel "1.8"
    ipr {
      withXml { provider ->
        def node = provider.asNode()
        node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
      }
    }
  }
}